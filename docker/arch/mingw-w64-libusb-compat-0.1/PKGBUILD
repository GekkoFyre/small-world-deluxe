#
# Maintainer: Christopher McGill of GekkoFyre Networks <phobos.gekko@gekkofyre.io>
# https://gekkofyre.io/
#

_reponame=libusb-compat-0.1
pkgname=mingw-w64-$_reponame
pkgver=0.1.7
pkgrel=1
pkgdesc='A compatibility layer allowing applications written for libusb-0.1 to work with libusb-1.0. This is the mingw-w64 version.'
arch=('any')
url='https://github.com/libusb/libusb-compat-0.1/'
license=('LGPL')
makedepends=('mingw-w64-gcc' 'mingw-w64-configure' 'mingw-w64-libusb')
source=(libusb-compat-$pkgver.tar.gz::https://github.com/libusb/libusb-compat-0.1/releases/download/v0.1.7/libusb-compat-$pkgver.tar.gz)
md5sums=('bfaba9121a9cadd1c377e68c589b0e6c')
options=(!buildflags staticlibs !strip !buildflags)

_architectures='i686-w64-mingw32 x86_64-w64-mingw32'

build() {
  cd "${srcdir}/libusb-compat-${pkgver}"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/libusb-compat-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
