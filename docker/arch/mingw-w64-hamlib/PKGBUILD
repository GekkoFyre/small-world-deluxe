#
# Maintainer: Christopher McGill of GekkoFyre Networks <phobos.gekko@gekkofyre.io>
# https://gekkofyre.io/
#

_reponame=hamlib
pkgname=mingw-w64-$_reponame
pkgver=3.3
pkgrel=1
pkgdesc='Ham radio control library (mingw-w64).'
arch=('any')
url='https://hamlib.github.io/'
license=('LGPL')
makedepends=('mingw-w64-gcc' 'mingw-w64-configure')
source=(hamlib-$pkgver.tar.gz::https://github.com/Hamlib/Hamlib/releases/download/3.3/hamlib-$pkgver.tar.gz)
md5sums=('2faa2894a61ed0ef307740aa6e3b73e4')
options=(!buildflags staticlibs !strip !buildflags)

_architectures='i686-w64-mingw32 x86_64-w64-mingw32'

build() {
  cd "${srcdir}/hamlib-${pkgver}"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure \
      --with-cxx-binding \
      --disable-html-matrix
    make CFLAGS='-fno-stack-protector -lssp'
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/hamlib-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    rm -r "$pkgdir/usr/${_arch}/share"
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
